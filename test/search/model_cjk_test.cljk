(ns search.model-cjk-test
  "CJK tokenization: Japanese has no spaces, so an unsegmented CJK run
  token ('欧州連合理事会') never intersects a short query ('理事会') under
  posting-AND. Overlapping bigrams are the minimal lossless segmentation
  that keeps retrieve O(df)."
  (:require [clojure.test :refer [deftest is]]
            [search.model :as model]
            [search.postings :as postings]))

(deftest kanji-runs-are-bigrams
  (is (= ["欧州" "州連" "連合" "合理" "理事" "事会"]
         (model/tokenize "欧州連合理事会"))))

(deftest short-cjk-query-still-bigams
  (is (= ["三豊"] (model/tokenize "三豊"))))

(deftest ascii-is-unchanged
  (is (= ["postings" "and" "intersect"]
         (model/tokenize "Postings AND intersect"))))

(deftest mixed-kana-kanji-tokens-bigram-too
  "A body token like '資本金の額' mixes kanji runs with hiragana tails.
  The kanji run must still produce bigrams, or a kanji-only query
  ('資本金') can never intersect the document."
  (is (some #{"資本"} (model/tokenize "資本金の額の1000分の7")))
  (is (some #{"本金"} (model/tokenize "資本金の額の1000分の7"))))

(deftest katakana-bigrams-with-long-vowel-mark
  (is (some #{"ミツ"} (model/tokenize "ミツトヨ"))))

(deftest cjk-postings-and-finds-substring-title
  (let [idx (postings/build
             [(model/document "eu-council"
                              {:search/title "欧州連合理事会"
                               :search/url "https://wiki.kotobase.net/item/eu-council"})])]
    (is (= ["eu-council"] (:hits (postings/query idx "理事会"))))
    (is (= ["eu-council"] (:hits (postings/query idx "欧州連合理事会"))))))

(deftest cjk-and-across-two-documents
  (let [idx (postings/build
             [(model/document "a" {:search/title "香川県発注土木一式工事 入札談合事件"})
              (model/document "b" {:search/title "英国 入札談合事件"})])]
    (is (= ["a" "b"] (:hits (postings/query idx "入札談合"))))
    (is (= ["a"] (:hits (postings/query idx "香川県 談合"))))))

(deftest cjk-ranking-scores-title-over-body
  (let [idx (-> (model/index)
                (model/add-document
                 (model/document "titled"
                                 {:search/title "理事会"
                                  :search/body "雑音"}))
                (model/add-document
                 (model/document "b"
                                 {:search/title "無関係"
                                  :search/body "理事会についての本文"})))
        hits (model/search idx "理事会")]
    (is (= "titled" (:search/id (first hits))))
    (is (< (:search/score (second hits))
           (:search/score (first hits))))))
